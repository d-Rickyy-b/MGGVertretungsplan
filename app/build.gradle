apply plugin: 'com.android.application'
apply plugin: 'com.github.kt3k.coveralls'
apply from: '../jacoco.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'


    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "de.aurora.mggvertretungsplan"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 25
        versionName "3.1.5"
        multiDexEnabled true
    }

    compileOptions.encoding = "utf-8"

    signingConfigs {
        release
    }

    def isRunningOnTravis = System.getenv("CI") == "true"

    if (isRunningOnTravis) {
        // configure keystore
        signingConfigs.release.storeFile = file("../keystore.ks")
        signingConfigs.release.storePassword = System.getenv("keystore_password")
        signingConfigs.release.keyAlias = System.getenv("keystore_alias")
        signingConfigs.release.keyPassword = System.getenv("keystore_alias_password")
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (isRunningOnTravis) {
                signingConfig signingConfigs.release
            }
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            testCoverageEnabled = true
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // Required -- JUnit 4 framework (Unit tests)
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.22'

    // Support libraries
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-alpha01'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference:1.1.0-alpha01'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.core:core:1.1.0-alpha02'

    //implementation "androidx.work:work-runtime:1.0.0-alpha09"
    implementation "android.arch.work:work-runtime:1.0.0-alpha12"

    // Other libraries
    implementation 'org.jsoup:jsoup:1.10.2'
    implementation 'com.github.paolorotolo:appintro:v5.1.0'
    implementation 'com.thebluealliance:spectrum:0.7.1'
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/testDebugUnitTestCoverage/testDebugUnitTestCoverage.xml"
}
tasks.coveralls {
    dependsOn 'testDebugUnitTestCoverage'
    onlyIf { System.env.'CI' }
}
