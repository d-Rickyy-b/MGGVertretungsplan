apply plugin: 'com.android.application'
apply plugin: 'com.github.kt3k.coveralls'
apply from: '../jacoco.gradle'

android {
    compileSdkVersion 26
    buildToolsVersion '27.0.3'


    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "de.aurora.mggvertretungsplan"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 25
        versionName "3.1.5"
        multiDexEnabled true
    }

    compileOptions.encoding = "utf-8"

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            testCoverageEnabled = true
        }
    }

    def isRunningOnTravis = System.getenv("CI") == "true"

    if (isRunningOnTravis) {
        // configure keystore
        signingConfigs.release.storeFile = file("../keystore.ks.enc")
        signingConfigs.release.storePassword = System.getenv("keystore_password")
        signingConfigs.release.keyAlias = System.getenv("keystore_alias")
        signingConfigs.release.keyPassword = System.getenv("keystore_alias_password")
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // Required -- JUnit 4 framework (Unit tests)
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.22'

    // Support libraries
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:preference-v7:26.1.0'
    implementation 'com.android.support:customtabs:26.1.0'

    // Other libraries
    implementation 'org.jsoup:jsoup:1.10.2'
    implementation 'com.github.paolorotolo:appintro:4.1.0'
    implementation 'com.thebluealliance:spectrum:0.7.1'
}

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/testDebugUnitTestCoverage/testDebugUnitTestCoverage.xml"
}
tasks.coveralls {
    dependsOn 'testDebugUnitTestCoverage'
    onlyIf { System.env.'CI' }
}